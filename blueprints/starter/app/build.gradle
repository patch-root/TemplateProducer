import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.plugin.mpp.KotlinNativeTarget


plugins {
    alias(libs.plugins.appPlatform)
    alias(libs.plugins.androidApplication)
    alias(libs.plugins.kotlinMultiplatform)
}

appPlatform {
    enableComposeUi true
    enableKotlinInject true
    enableModuleStructure true
    enableMoleculePresenters true
    addImplModuleDependencies true
}

kotlin {
    androidTarget {
        compilerOptions {
            jvmTarget = JvmTarget.JVM_11
        }
    }

    iosX64()
    iosArm64()
    iosSimulatorArm64()

    targets.withType(KotlinNativeTarget).configureEach {
        binaries.framework {
            baseName = "TemplateApp"
            isStatic = true
        }
    }

    wasmJs {
        binaries.executable()

        browser {
            outputModuleName = project.name.replace("-", "")
            commonWebpackConfig {
                outputFileName = "template-app.js"
            }
        }
    }
}

android {
    compileSdk = libs.versions.android.compileSdk.get().toInteger()

    defaultConfig {
        applicationId = "software.amazon.app.platform.template"
        versionCode = 1
        versionName = "1.0"
        minSdk = libs.versions.android.minSdk.get().toInteger()
        targetSdk = libs.versions.android.targetSdk.get().toInteger()
    }

    packagingOptions {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }

    buildTypes {
        release {
            minifyEnabled = false
        }
    }
}

dependencies {
    commonMainImplementation libs.androidx.lifecycle.viewmodel

    commonMainImplementation project(':navigation:impl')
    commonMainImplementation project(':navigation:public')
    commonMainImplementation project(':templates:impl')
    commonMainImplementation project(':templates:public')

    androidMainImplementation libs.androidx.activity.compose
}